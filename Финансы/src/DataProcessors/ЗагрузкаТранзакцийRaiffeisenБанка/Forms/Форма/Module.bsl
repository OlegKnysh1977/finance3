&НаСервереБезКонтекста
Функция СтрокаВДату(СтрДанные)
	Если ПустаяСтрока(СтрДанные) Тогда
		Возврат Дата(1,1,1);
	Иначе
		Возврат Дата(Число(Сред(СтрДанные,7,4)), Число(Сред(СтрДанные,4,2)), Число(Сред(СтрДанные,1,2)));
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция СтрокаВЧисло(СтрДанные)
	Возврат Число(СтрЗаменить(СтрДанные," ",""));
КонецФункции

&НаСервере
Процедура ЗагрузитьФайлCSV(ИмяВременногоТекстовогоФайла)
	
	_ТекущаяДата_ = ТекущаяДата();
	
	НачатьТранзакцию();
	
	Текст = Новый ЧтениеТекста(ИмяВременногоТекстовогоФайла, КодировкаТекста.ANSI);
	
	Стр = СокрЛП(Текст.ПрочитатьСтроку());
	Сч = 0;
	
	Пока Стр <> Неопределено Цикл
		
		Сч = Сч + 1;
		
		Если Сч <> 1 Тогда
		
			НомерТранзакции = Справочники.Транзакции.ПолучитьМаксимальныйНомерТранзакцииПоКлючу(Стр);
			
			ОбъектКлюч = Справочники.Транзакции.СоздатьЭлемент();
			ОбъектКлюч.КлючТранзакции = Стр;
			ОбъектКлюч.НомерТранзакции = НомерТранзакции + 1;
			ОбъектКлюч.Записать();
			
			СсылкаНаКлюч = ОбъектКлюч.Ссылка;
			
			НаборЗаписей = РегистрыСведений.ИсторияТранзакций.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.СчетУчета.Установить(Объект.СчетУчета);
			НаборЗаписей.Отбор.Транзакция.Установить(СсылкаНаКлюч);
			
			МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Стр, ";");
			
			НоваяЗапись = НаборЗаписей.Добавить();

			НоваяЗапись.СчетУчета = Объект.СчетУчета;
			НоваяЗапись.Транзакция = СсылкаНаКлюч;
			
			//Дата операции;Дата проводки;Описание;Продавец;Город;Страна;Валюта операции;Сумма в валюте операции;Валюта счета;Сумма в валюте счета
			//24.12.2015 00:00;24.12.2015 00:00;RBA ATM 18061		 ST-PETERSBURG RU;RBA ATM 18061;ST-PETERSBURG;RU ;RUB;5 000.00;RUB;5 000.00
			// Было
			//10.01.2014;14.01.2014;RUR;PYATEROCHKA;Grocery Stores, Supermarkets;SANKT-PETERBU;Россия;-39.00;39.00
			
			НоваяЗапись.ДатаЗагрузки			= _ТекущаяДата_;
			НоваяЗапись.ДатаТранзакции			= СтрокаВДату(МассивПодстрок[0]);
			НоваяЗапись.ДатаФиксацииТранзакции	= СтрокаВДату(МассивПодстрок[1]);
			//НоваяЗапись.Валюта					= МассивПодстрок[2];
			//НоваяЗапись.МестоПлатежа1			= МассивПодстрок[3];
			//НоваяЗапись.МестоПлатежа2			= МассивПодстрок[4];
			//НоваяЗапись.Город					= МассивПодстрок[5];
			//НоваяЗапись.Страна					= МассивПодстрок[6];
			//НоваяЗапись.Сумма1					= СтрокаВЧисло(МассивПодстрок[7]);
			//НоваяЗапись.Сумма2					= СтрокаВЧисло(МассивПодстрок[8]);
			НоваяЗапись.Валюта					= МассивПодстрок[6];
			НоваяЗапись.МестоПлатежа1			= МассивПодстрок[2];
			НоваяЗапись.МестоПлатежа2			= МассивПодстрок[3];
			НоваяЗапись.Город					= МассивПодстрок[4];
			НоваяЗапись.Страна					= МассивПодстрок[5];
			НоваяЗапись.Сумма1					= СтрокаВЧисло(МассивПодстрок[7]);
			НоваяЗапись.Сумма2					= СтрокаВЧисло(МассивПодстрок[9]);

			НаборЗаписей.Записать();
			
		КонецЕсли;
		
		Стр = Текст.ПрочитатьСтроку();
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();

КонецПроцедуры

&НаСервере
Процедура ОбработкаФайловНаСервере(МассивОписанийПереданныхФайлов)
	
	Для каждого ТекОписанийПереданныхФайлов Из МассивОписанийПереданныхФайлов Цикл
		
		Если ЭтоАдресВременногоХранилища(ТекОписанийПереданныхФайлов.Хранение) Тогда
			
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(ТекОписанийПереданныхФайлов.Хранение);
			СтрИмяВременногоФайла = ПолучитьИмяВременногоФайла("csv");
			ДвоичныеДанные.Записать(СтрИмяВременногоФайла);
			
			ЗагрузитьФайлCSV(СтрИмяВременногоФайла);
			
			УдалитьФайлы(СтрИмяВременногоФайла);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияФайловНаСервер(МассивОписанийПереданныхФайлов, Параметр2) Экспорт
	
	ОбработкаФайловНаСервере(МассивОписанийПереданныхФайлов);
	
	ПоказатьПредупреждение(,"Загрузка выполнена.");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДиалогаФайла(МассивИменФайлов, Параметр2) Экспорт
	
	МассивФайлов = Новый Массив;
	
	Для каждого СтрИмяФайла Из МассивИменФайлов Цикл
		МассивФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(СтрИмяФайла, ));
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПомещенияФайловНаСервер", ЭтотОбъект);
	НачатьПомещениеФайлов(Оповещение, МассивФайлов,,Ложь);
	
КонецПроцедуры
	
&НаКлиенте
Процедура КомандаЗагрузитьДанные(Команда)
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = "Файлы обмена (*.csv)|*.csv";
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораДиалогаФайла", ЭтотОбъект);
	
	ДиалогОткрытияФайла.Показать(Оповещение);
	
КонецПроцедуры
