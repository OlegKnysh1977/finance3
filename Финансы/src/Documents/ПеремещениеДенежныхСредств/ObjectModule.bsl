
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ДатаЗачисления = Дата;
	
	СчетУчетаСписания = Константы.ОсновнойСчетУчета.Получить();
	Если ЗначениеЗаполнено(СчетУчетаСписания) Тогда
		ВалютаСписания = СчетУчетаСписания.ВалютаСчета;
	КонецЕсли;
	
	СчетУчетаЗачисления = Константы.ОсновнойСчетУчета.Получить();
	Если ЗначениеЗаполнено(СчетУчетаЗачисления) Тогда
		ВалютаЗачисления = СчетУчетаЗачисления.ВалютаСчета;
	КонецЕсли;
	
	СтатьяПеремещения = Константы.ОсновнойСчетУчета.Получить();
	МестоПеремещенияДенежныхСредств = Константы.ОсновноеМестоРасходаДенежныхСредств.Получить();
	
КонецПроцедуры

Процедура ЗаполнитьТаблицыДвижений(ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ПеремещениеДенежныхСредств.Дата КАК Период,
		|	ПеремещениеДенежныхСредств.СчетУчетаСписания КАК СчетУчета,
		|	ПеремещениеДенежныхСредств.СуммаСписания КАК Сумма
		|ИЗ
		|	Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
		|ГДЕ
		|	ПеремещениеДенежныхСредств.Ссылка = &Ссылка
		|	И ПеремещениеДенежныхСредств.Дата <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	ПеремещениеДенежныхСредств.ДатаЗачисления,
		|	ПеремещениеДенежныхСредств.СчетУчетаЗачисления,
		|	ПеремещениеДенежныхСредств.СуммаЗачисления
		|ИЗ
		|	Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
		|ГДЕ
		|	ПеремещениеДенежныхСредств.Ссылка = &Ссылка
		|	И ПеремещениеДенежныхСредств.ДатаЗачисления <> ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеДанные.Период,
		|	ВсеДанные.ВалютаДвижения,
		|	ВсеДанные.СчетУчета,
		|	ВсеДанные.СтатьяДоходовРасходов,
		|	ВсеДанные.МестоРасходовДенежныхСредств,
		|	СУММА(ВсеДанные.СуммаПрихода) КАК СуммаПрихода,
		|	СУММА(ВсеДанные.СуммаРасхода) КАК СуммаРасхода
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПеремещениеДенежныхСредств.Дата КАК Период,
		|		ПеремещениеДенежныхСредств.ВалютаСписания КАК ВалютаДвижения,
		|		ПеремещениеДенежныхСредств.СчетУчетаСписания КАК СчетУчета,
		|		ПеремещениеДенежныхСредств.СтатьяПеремещения КАК СтатьяДоходовРасходов,
		|		0 КАК СуммаПрихода,
		|		ПеремещениеДенежныхСредств.СуммаСписания КАК СуммаРасхода,
		|		ЗНАЧЕНИЕ(Справочник.МестаРасходовДенежныхСредств.ПустаяСсылка) КАК МестоРасходовДенежныхСредств
		|	ИЗ
		|		Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
		|	ГДЕ
		|		ПеремещениеДенежныхСредств.Ссылка = &Ссылка
		|		И ПеремещениеДенежныхСредств.Дата <> ДАТАВРЕМЯ(1, 1, 1)
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ПеремещениеДенежныхСредств.ДатаЗачисления,
		|		ПеремещениеДенежныхСредств.ВалютаЗачисления КАК ВалютаДвижения,
		|		ПеремещениеДенежныхСредств.СчетУчетаЗачисления,
		|		ПеремещениеДенежныхСредств.СтатьяПеремещения,
		|		ПеремещениеДенежныхСредств.СуммаЗачисления,
		|		0,
		|		ПеремещениеДенежныхСредств.МестоПеремещенияДенежныхСредств
		|	ИЗ
		|		Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
		|	ГДЕ
		|		ПеремещениеДенежныхСредств.Ссылка = &Ссылка
		|		И ПеремещениеДенежныхСредств.ДатаЗачисления <> ДАТАВРЕМЯ(1, 1, 1)) КАК ВсеДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВсеДанные.Период,
		|	ВсеДанные.ВалютаДвижения,
		|	ВсеДанные.СчетУчета,
		|	ВсеДанные.СтатьяДоходовРасходов,
		|	ВсеДанные.МестоРасходовДенежныхСредств";

	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();

	ТаблицаДляОстатков				= МассивРезультатов[0].Выгрузить();
	ТаблицаДвиженийДенежныхСредств	= МассивРезультатов[1].Выгрузить();

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
Перем ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств;
	
	ЗаполнитьТаблицыДвижений(ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств);
	
	Если ТаблицаДляОстатков.Количество() <> 0 Тогда
		
		Движения.ОстаткиДенежныхСредств.Записывать = Истина;
		Движения.ОстаткиДенежныхСредств.Загрузить(ТаблицаДляОстатков);
		
	КонецЕсли;
	
	Если ТаблицаДвиженийДенежныхСредств.Количество() <> 0 Тогда
		
		Движения.ДвиженияДенежныхСредств.Записывать = Истина;
		Движения.ДвиженияДенежныхСредств.Загрузить(ТаблицаДвиженийДенежныхСредств);
		
	КонецЕсли;
	
КонецПроцедуры
