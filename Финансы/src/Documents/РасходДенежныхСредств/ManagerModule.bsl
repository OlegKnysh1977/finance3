Функция ПолучитьДанныеЗаполненияПоСчетуНаОплату(СчетНаОплатуСсылка, СтруктураШапка, МассивСтрокТаблицыРасшифровка) Экспорт
	
	СтруктураШапка = Новый Структура("МестоРасходовДенежныхСредств,СчетУчета,ВалютаДокумента,СразуСписывать");
	МассивСтрокТаблицыРасшифровка = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетНаОплату.СчетУчета,
		|	СчетНаОплату.МестоРасходовДенежныхСредств,
		|	СчетНаОплату.ВалютаДокумента КАК ВалютаДокумента,
		|	Истина КАК СразуСписывать
		|ИЗ
		|	Документ.СчетНаОплату КАК СчетНаОплату
		|ГДЕ
		|	СчетНаОплату.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетНаОплатуРасшифровка.НомерСтроки КАК НомерСтроки,
		|	СчетНаОплатуРасшифровка.СтатьяРасходов,
		|	СчетНаОплатуРасшифровка.Цена,
		|	СчетНаОплатуРасшифровка.Количество,
		|	СчетНаОплатуРасшифровка.Сумма,
		|	СчетНаОплатуРасшифровка.СуммаВзаиморасчетов
		|ИЗ
		|	Документ.СчетНаОплату.Расшифровка КАК СчетНаОплатуРасшифровка
		|ГДЕ
		|	СчетНаОплатуРасшифровка.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Запрос.УстановитьПараметр("Ссылка", СчетНаОплатуСсылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();

	ВыборкаШапки = МассивРезультатов[0].Выбрать();
	
	Если ВыборкаШапки.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураШапка, ВыборкаШапки, "МестоРасходовДенежныхСредств,СчетУчета,ВалютаДокумента,СразуСписывать");
	КонецЕсли;
	
	ВыборкаРасшифровки = МассивРезультатов[1].Выбрать();
	
	Пока ВыборкаРасшифровки.Следующий() Цикл
		СтруктураСтрокиРасшифровки = Новый Структура("СтатьяРасходов,Цена,Количество,Сумма,СуммаВзаиморасчетов");
		ЗаполнитьЗначенияСвойств(СтруктураСтрокиРасшифровки, ВыборкаРасшифровки, "СтатьяРасходов,Цена,Количество,Сумма,СуммаВзаиморасчетов");
		МассивСтрокТаблицыРасшифровка.Добавить(СтруктураСтрокиРасшифровки);
	КонецЦикла;
	

КонецФункции