Перем мВалютаРегламентированногоУчета Экспорт;

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
Перем СтруктураШапка, МассивСтрокТаблицыРасшифровка;
	
	Если ДанныеЗаполнения = Неопределено Тогда
		
		МестоРасходовДенежныхСредств = Константы.ОсновноеМестоРасходаДенежныхСредств.Получить();
		СчетУчета = Константы.ОсновнойСчетУчета.Получить();
		Если ЗначениеЗаполнено(СчетУчета) Тогда
			ВалютаДокумента = СчетУчета.ВалютаСчета;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СчетНаОплату") Тогда
		
		Документы.РасходДенежныхСредств.ПолучитьДанныеЗаполненияПоСчетуНаОплату(ДанныеЗаполнения, СтруктураШапка, МассивСтрокТаблицыРасшифровка);
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураШапка, "МестоРасходовДенежныхСредств,СчетУчета,ВалютаДокумента");
		Для Каждого ТекЭлементМассива Из МассивСтрокТаблицыРасшифровка Цикл
			НоваяСтрока = Расшифровка.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекЭлементМассива, "СтатьяРасходов,Цена,Количество,Сумма,СуммаВзаиморасчетов");
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Расшифровка.Итог("Сумма");
	
КонецПроцедуры

Процедура ЗаполнитьТаблицыДвижений(ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств, ТаблицаИсторияЦенПокупок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	РасходДенежныхСредств.Дата КАК Период,
		|	РасходДенежныхСредств.СчетУчета,
//		|	РасходДенежныхСредств.ДокументОснование,
		|	РасходДенежныхСредств.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.РасходДенежныхСредств КАК РасходДенежныхСредств
		|ГДЕ
		|	РасходДенежныхСредств.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходДенежныхСредств.Дата КАК Период,
		|	РасходДенежныхСредств.СчетУчета,
		|	РасходДенежныхСредств.ВалютаДокумента КАК ВалютаДвижения,
		|	РасходДенежныхСредствРасшифровка.СтатьяРасходов КАК СтатьяДоходовРасходов,
		|	РасходДенежныхСредств.МестоРасходовДенежныхСредств,
		|	СУММА(0) КАК СуммаПрихода,
		|	СУММА(РасходДенежныхСредствРасшифровка.Сумма) КАК СуммаРасхода
		|ИЗ
		|	Документ.РасходДенежныхСредств.Расшифровка КАК РасходДенежныхСредствРасшифровка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходДенежныхСредств КАК РасходДенежныхСредств
		|		ПО РасходДенежныхСредствРасшифровка.Ссылка = РасходДенежныхСредств.Ссылка
		|ГДЕ
		|	РасходДенежныхСредств.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходДенежныхСредств.Дата,
		|	РасходДенежныхСредств.ВалютаДокумента,
		|	РасходДенежныхСредств.СчетУчета,
		|	РасходДенежныхСредствРасшифровка.СтатьяРасходов,
		|	РасходДенежныхСредств.МестоРасходовДенежныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Выборка.Дата КАК Период,
		|	Выборка.МестоРасходовДенежныхСредств,
		|	Выборка.СтатьяРасходов,
		|	ВЫБОР
		|		КОГДА Выборка.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ Выборка.Сумма / Выборка.Количество
		|	КОНЕЦ КАК Цена
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасходДенежныхСредств.Дата КАК Дата,
		|		РасходДенежныхСредств.МестоРасходовДенежныхСредств КАК МестоРасходовДенежныхСредств,
		|		РасходДенежныхСредствРасшифровка.СтатьяРасходов КАК СтатьяРасходов,
		|		СУММА(РасходДенежныхСредствРасшифровка.Количество) КАК Количество,
		|		СУММА(РасходДенежныхСредствРасшифровка.Сумма) КАК Сумма
		|	ИЗ
		|		Документ.РасходДенежныхСредств.Расшифровка КАК РасходДенежныхСредствРасшифровка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходДенежныхСредств КАК РасходДенежныхСредств
		|			ПО РасходДенежныхСредствРасшифровка.Ссылка = РасходДенежныхСредств.Ссылка
		|	ГДЕ
		|		РасходДенежныхСредств.Ссылка = &Ссылка
		|	
		|	СГРУППИРОВАТЬ ПО
		|		РасходДенежныхСредств.Дата,
		|		РасходДенежныхСредств.МестоРасходовДенежныхСредств,
		|		РасходДенежныхСредствРасшифровка.СтатьяРасходов) КАК Выборка";

	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();

	ТаблицаДляОстатков				= МассивРезультатов[0].Выгрузить();
	ТаблицаДвиженийДенежныхСредств	= МассивРезультатов[1].Выгрузить();
	ТаблицаИсторияЦенПокупок		= МассивРезультатов[2].Выгрузить();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
Перем ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств, ТаблицаИсторияЦенПокупок;
	
	ЗаполнитьТаблицыДвижений(ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств, ТаблицаИсторияЦенПокупок);
	
	Если ТаблицаДляОстатков.Количество() <> 0 Тогда
		
		Движения.ОстаткиДенежныхСредств.Записывать = Истина;
		Движения.ОстаткиДенежныхСредств.Загрузить(ТаблицаДляОстатков);
		
	КонецЕсли;
	
	Если ТаблицаДвиженийДенежныхСредств.Количество() <> 0 Тогда
		
		Движения.ДвиженияДенежныхСредств.Записывать = Истина;
		Движения.ДвиженияДенежныхСредств.Загрузить(ТаблицаДвиженийДенежныхСредств);
		
	КонецЕсли;
	
	Если ТаблицаИсторияЦенПокупок.Количество() <> 0 Тогда
		
		Движения.ИсторияЦенПокупок.Записывать = Истина;
		Движения.ИсторияЦенПокупок.Загрузить(ТаблицаИсторияЦенПокупок);
		
	КонецЕсли;
	
	//Если ТипЗнч(ТаблицаДляОстатков.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплату") Тогда
	//	
	//	//Движения.ПланируемыеРасходы.Записывать = Истина;
	//	//Движения.ПланируемыеРасходы.Загрузить(ТаблицаДляОстатков);
	//	
	//КонецЕсли;
	
КонецПроцедуры

мВалютаРегламентированногоУчета = Константы.ОсновнаяВалютаУчета.Получить();
