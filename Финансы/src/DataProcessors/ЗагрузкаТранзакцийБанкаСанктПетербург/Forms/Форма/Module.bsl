&НаСервереБезКонтекста
Функция СтрокаВДату(СтрДанные)
	Если ПустаяСтрока(СтрДанные) Тогда
		Возврат Дата(1,1,1);
	Иначе
		Возврат Дата(Число(Сред(СтрДанные,7,4)), Число(Сред(СтрДанные,4,2)), Число(Сред(СтрДанные,1,2)));
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция СтрокаВЧисло(СтрДанные)
	Возврат Число(СтрЗаменить(СтрДанные," ",""));
КонецФункции

&НаСервере
Процедура ЗагрузитьФайлXLS(СтрИмяВременногоФайла)
	
	_ТекущаяДата_ = ТекущаяДата();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.Прочитать(СтрИмяВременногоФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	
	ОбластьSheet1 = ТабДокумент.Области.Найти("Sheet1");
	
	Если ОбластьSheet1 = Неопределено Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В файле MS Excel не найдена закладка с наименованием ""Sheet1"".");
		Возврат;
	
	КонецЕсли;
	
	ВысотаТаблицы = ТабДокумент.ВысотаТаблицы;
	ШиринаТаблицы = ТабДокумент.ШиринаТаблицы;
	
	ВерхОбласти = ОбластьSheet1.Верх;
	НизОбласти = ОбластьSheet1.Низ;
	
	Если ВысотаТаблицы <= 11 Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Высота таблицы файла MS Excel должна быть больше 11 строк.");
		Возврат;
	
	КонецЕсли; 
	
	Если ШиринаТаблицы <> 6 Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Количество столбцов таблицы файла MS Excel должна быть равно 6 колонок.");
		Возврат;
	
	КонецЕсли;
	
	// Дата	"Счёт плательщика/получателя"	Плательщик / Получатель	Операция	Сумма (RUB)
	
	Ошибка = Ложь;

	Если ТабДокумент.Область(ВерхОбласти + 6, 1, ВерхОбласти + 6, 1).Текст <> "Дата" Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В закладке ""Sheet1"", в строке "+(ВерхОбласти + 6)+", в столбце 1 ожидается заголовок ""Дата"".");
		Ошибка = Истина;
	
	КонецЕсли;
	
	Если ТабДокумент.Область(ВерхОбласти + 6, 2, ВерхОбласти + 6, 2).Текст <> ("Счёт"+Символы.ПС+"плательщика/получателя") Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В закладке ""Sheet1"", в строке "+(ВерхОбласти + 6)+", в столбце 2 ожидается заголовок ""Счёт плательщика/получателя"".");
		Ошибка = Истина;
	
	КонецЕсли;
	
	Если ТабДокумент.Область(ВерхОбласти + 6, 3, ВерхОбласти + 6, 3).Текст <> "Плательщик / Получатель" Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В закладке ""Sheet1"", в строке "+(ВерхОбласти + 6)+", в столбце 3 ожидается заголовок ""Плательщик / Получатель"".");
		Ошибка = Истина;
	
	КонецЕсли;
	
	Если ТабДокумент.Область(ВерхОбласти + 6, 4, ВерхОбласти + 6, 4).Текст <> "Операция" Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В закладке ""Sheet1"", в строке "+(ВерхОбласти + 6)+", в столбце 4 ожидается заголовок ""Операция"".");
		Ошибка = Истина;
	
	КонецЕсли;
	
	Если ТабДокумент.Область(ВерхОбласти + 6, 5, ВерхОбласти + 6, 5).Текст <> "Сумма (RUB)" Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В закладке ""Sheet1"", в строке "+(ВерхОбласти + 6)+", в столбце 5 ожидается заголовок ""Сумма (RUB)"".");
		Ошибка = Истина;
	
	КонецЕсли;
	
	Если Ошибка Тогда
	
		Возврат;
	
	КонецЕсли;

	НачатьТранзакцию();

	НаборЗаписей = РегистрыСведений.ИсторияТранзакцийНовая.СоздатьНаборЗаписей();
	
	ВиртуальнаяТаблица = НаборЗаписей.ВыгрузитьКолонки();
	
	НомерТранзакции = 0;
	
	Для НомерСтроки = ВерхОбласти + 7 По НизОбласти Цикл
	
		ДатаТранзакции = ТабДокумент.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
		
		Если ЗначениеЗаполнено(ДатаТранзакции) Тогда
			
			НомерТранзакции = НомерТранзакции + 1;
		
			НоваяСтрока = ВиртуальнаяТаблица.Добавить();
			НоваяСтрока.СчетУчета				= Объект.СчетУчета;
			НоваяСтрока.ДатаТранзакции			= СтрокаВДату(ДатаТранзакции);
			НоваяСтрока.НомерТранзакции			= НомерТранзакции;
			НоваяСтрока.ДатаФиксацииТранзакции	= НоваяСтрока.ДатаТранзакции;
			НоваяСтрока.Валюта					= "RUR";
			НоваяСтрока.СчетПолучателя			= ТабДокумент.Область(НомерСтроки, 2, НомерСтроки, 2).Текст;
			НоваяСтрока.МестоПлатежа2			= ТабДокумент.Область(НомерСтроки, 3, НомерСтроки, 3).Текст;
			НоваяСтрока.Комментарий				= ТабДокумент.Область(НомерСтроки, 4, НомерСтроки, 4).Текст;
			НоваяСтрока.Сумма1					= ТабДокумент.Область(НомерСтроки, 5, НомерСтроки, 5).Текст;
			НоваяСтрока.Сумма2					= ТабДокумент.Область(НомерСтроки, 5, НомерСтроки, 5).Текст;
			НоваяСтрока.ДатаЗагрузки			= _ТекущаяДата_;
			
		КонецЕсли; 
	
	КонецЦикла; 
	
	МассивДат = Новый Массив;
	ОбщегоНазначения.ЗаполнитьМассивУникальнымиЗначениями(МассивДат, ВиртуальнаяТаблица.ВыгрузитьКолонку("ДатаТранзакции"));
	
	ДопТаблица = ВиртуальнаяТаблица.СкопироватьКолонки();
	
	Для каждого ТекДата Из МассивДат Цикл
	
		НомерТранзакции = 0;
		ДопТаблица.Очистить();

		МассивСтрок = ВиртуальнаяТаблица.НайтиСтроки(Новый Структура("ДатаТранзакции", ТекДата));
		
		Для каждого НоваяСтрока Из МассивСтрок Цикл
		
			НомерТранзакции = НомерТранзакции + 1;
			
			НоваяСтрока.НомерТранзакции			= НомерТранзакции;
			
			МассивПодстрок = СтрРазделить(НоваяСтрока.МестоПлатежа2, ",", Истина);
			
			Если МассивПодстрок.Количество() >= 3 Тогда
			
				НоваяСтрока.Страна = МассивПодстрок[МассивПодстрок.ВГраница()];
				МассивПодстрок.Удалить(МассивПодстрок.ВГраница());
				НоваяСтрока.Город = МассивПодстрок[МассивПодстрок.ВГраница()];
				МассивПодстрок.Удалить(МассивПодстрок.ВГраница());
				
				НоваяСтрока.МестоПлатежа1 = СтрСоединить(МассивПодстрок, ",");
				
			Иначе
				
				НоваяСтрока.МестоПлатежа1 = НоваяСтрока.МестоПлатежа2;
			
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ДопТаблица.Добавить(), НоваяСтрока);
		
		КонецЦикла; 
		
		НаборЗаписей = РегистрыСведений.ИсторияТранзакцийНовая.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СчетУчета.Установить(Объект.СчетУчета);
		НаборЗаписей.Отбор.ДатаТранзакции.Установить(ТекДата);
		
		НаборЗаписей.Записать();
		
		НаборЗаписей.Загрузить(ДопТаблица);
		НаборЗаписей.Записать();
	
	КонецЦикла; 
	
	ТабДокумент = Неопределено;

	ЗафиксироватьТранзакцию();

КонецПроцедуры

&НаСервере
Процедура ОбработкаФайловНаСервере(МассивОписанийПереданныхФайлов)
	
	Для каждого ТекОписанийПереданныхФайлов Из МассивОписанийПереданныхФайлов Цикл
		
		Если ЭтоАдресВременногоХранилища(ТекОписанийПереданныхФайлов.Хранение) Тогда
			
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(ТекОписанийПереданныхФайлов.Хранение);
			СтрИмяВременногоФайла = ПолучитьИмяВременногоФайла("xls");
			ДвоичныеДанные.Записать(СтрИмяВременногоФайла);
			
			ЗагрузитьФайлXLS(СтрИмяВременногоФайла);
			
			УдалитьФайлы(СтрИмяВременногоФайла);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияФайловНаСервер(МассивОписанийПереданныхФайлов, Параметр2) Экспорт
	
	ОбработкаФайловНаСервере(МассивОписанийПереданныхФайлов);
	
	ПоказатьПредупреждение(,"Загрузка выполнена.");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДиалогаФайла(МассивИменФайлов, Параметр2) Экспорт
	
	МассивФайлов = Новый Массив;
	
	Для каждого СтрИмяФайла Из МассивИменФайлов Цикл
		МассивФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(СтрИмяФайла, ));
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПомещенияФайловНаСервер", ЭтотОбъект);
	НачатьПомещениеФайлов(Оповещение, МассивФайлов,,Ложь);
	
КонецПроцедуры
	
&НаКлиенте
Процедура КомандаЗагрузитьДанные(Команда)
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = "Файлы обмена банка Санкт-Петербург (*.xls)|*.xls";
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораДиалогаФайла", ЭтотОбъект);
	
	ДиалогОткрытияФайла.Показать(Оповещение);
	
КонецПроцедуры
