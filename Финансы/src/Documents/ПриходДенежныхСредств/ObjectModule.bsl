
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СчетУчета = Константы.ОсновнойСчетУчета.Получить();
	Если ЗначениеЗаполнено(СчетУчета) Тогда
		ВалютаДокумента = СчетУчета.ВалютаСчета;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Расшифровка.Итог("Сумма");
	
КонецПроцедуры

Процедура ЗаполнитьТаблицыДвижений(ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ПриходДенежныхСредств.Дата КАК Период,
		|	ПриходДенежныхСредств.СчетУчета,
		|	ПриходДенежныхСредств.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.ПриходДенежныхСредств КАК ПриходДенежныхСредств
		|ГДЕ
		|	ПриходДенежныхСредств.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходДенежныхСредств.Дата КАК Период,
		|	ПриходДенежныхСредств.СчетУчета,
		|	ПриходДенежныхСредств.ВалютаДокумента КАК ВалютаДвижения,
		|	ПриходДенежныхСредствРасшифровка.СтатьяДоходов КАК СтатьяДоходовРасходов,
		|	СУММА(ПриходДенежныхСредствРасшифровка.Сумма) КАК СуммаПрихода,
		|	СУММА(0) КАК СуммаРасхода,
		|	ЗНАЧЕНИЕ(Справочник.МестаРасходовДенежныхСредств.ПустаяСсылка) КАК МестоРасходовДенежныхСредств
		|ИЗ
		|	Документ.ПриходДенежныхСредств.Расшифровка КАК ПриходДенежныхСредствРасшифровка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходДенежныхСредств КАК ПриходДенежныхСредств
		|		ПО ПриходДенежныхСредствРасшифровка.Ссылка = ПриходДенежныхСредств.Ссылка
		|ГДЕ
		|	ПриходДенежныхСредств.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходДенежныхСредств.Дата,
		|	ПриходДенежныхСредств.ВалютаДокумента,
		|	ПриходДенежныхСредств.СчетУчета,
		|	ПриходДенежныхСредствРасшифровка.СтатьяДоходов";

	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();

	ТаблицаДляОстатков				= МассивРезультатов[0].Выгрузить();
	ТаблицаДвиженийДенежныхСредств	= МассивРезультатов[1].Выгрузить();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
Перем ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств;
	
	ЗаполнитьТаблицыДвижений(ТаблицаДляОстатков, ТаблицаДвиженийДенежныхСредств);
	
	Если ТаблицаДляОстатков.Количество() <> 0 Тогда
		
		Движения.ОстаткиДенежныхСредств.Записывать = Истина;
		Движения.ОстаткиДенежныхСредств.Загрузить(ТаблицаДляОстатков);
		
	КонецЕсли;
	
	Если ТаблицаДвиженийДенежныхСредств.Количество() <> 0 Тогда
		
		Движения.ДвиженияДенежныхСредств.Записывать = Истина;
		Движения.ДвиженияДенежныхСредств.Загрузить(ТаблицаДвиженийДенежныхСредств);
		
	КонецЕсли;
	
КонецПроцедуры
